<?xml version="1.0" encoding="UTF-8"?>
<!--
	CDDL HEADER START The contents of this file are subject to the terms
	of the Common Development and Distribution License, Version 1.0 only
	(the "License"). You may not use this file except in compliance with
	the License. You can obtain a copy of the license at
	license/ESCIDOC.LICENSE or http://www.escidoc.de/license. See the
	License for the specific language governing permissions and
	limitations under the License. When distributing Covered Code, include
	this CDDL HEADER in each file and include the License file at
	license/ESCIDOC.LICENSE. If applicable, add the following below this
	CDDL HEADER, with the fields enclosed by brackets "[]" replaced with
	your own identifying information: Portions Copyright [yyyy] [name of
	copyright owner] CDDL HEADER END Copyright 2006-2010
	Fachinformationszentrum Karlsruhe Gesellschaft für
	wissenschaftlich-technische Information mbH and Max-Planck-
	Gesellschaft zur Förderung der Wissenschaft e.V. All rights reserved.
	Use is subject to license terms.
-->
<!--
	Runtime Functions for the transformations from eSciDoc Citation Style Configuration Language 
	to the Citation Style Execution XSLT 
	See http://colab.mpdl.mpg.de/mediawiki/CitationStyleConfigurationConcept
	and https://zim02.gwdg.de/repos/common/trunk/common_services/citationmanager/src/main/resources/Schemas/citation-style.xsd
	Author: Vlad Makarenko (initial creation) $Author$ (last changed)
	$Revision$ $LastChangedDate$
-->
<functions 
	xmlns="http://www.escidoc.de/citationstyle" 
	xmlns:xsl="http://www.w3.org/1999/XSL/Transform"
	xmlns:func="http://www.escidoc.de/citationstyle/functions"
	xmlns:functx="http://www.functx.com"
	xmlns:jfunc="java:de.mpg.escidoc.services.citationmanager.utils.XsltHelper"	
	>
	
	<!-- RUNTIME FUNCTIONS -->
	
	<xsl:function name="func:get_year">
		<xsl:param name="date" />
		<xsl:value-of select="substring($date,1,4)" />
	</xsl:function>
	
	<xsl:function name="func:get_month">
		<xsl:param name="date" />
		<xsl:value-of select="substring($date,6,2)" />
	</xsl:function>
	
	<xsl:function name="func:get_month_name">
		<xsl:param name="date" />
		<xsl:variable name="months">
			<m n="0?1">January</m>
			<m n="0?2">February</m>
			<m n="0?3">March</m>
			<m n="0?4">April</m>
			<m n="0?5">May</m>
			<m n="0?6">June</m>
			<m n="0?7">July</m>
			<m n="0?8">August</m>
			<m n="0?9">September</m>
			<m n="10">October</m>
			<m n="11">November</m>
			<m n="12">December</m>
		</xsl:variable>
		<xsl:value-of select=" 
			$months/m[
				matches(
					tokenize($date, '-')[2], @n
				) 
			]
		" />
	</xsl:function>
	
	<xsl:function name="func:get_initials">
		<xsl:param name="str" />
		<xsl:variable name="delim"
			select="if (contains ($str, '-')) then '-' else ' '" />
		<xsl:for-each select="tokenize(normalize-space ($str), '\s+|\.\s+|\-\s*')">
			<xsl:value-of
				select="concat(substring (., 1, 1), if (position()!=last())then concat ('.', $delim) else '.')" />
		</xsl:for-each>
	</xsl:function>
	
	<!-- Postprocessing: clean citation -->
	<xsl:function name="func:cleanCitation">
		<xsl:param name="str" />
			<xsl:value-of select="
				normalize-space (
				functx:replace-multi (
					$str,
					( '([.,?!:;])\s*(&lt;[/]span&gt;)\s*\1',	'([.,?!:;])\s*\1',	'\.&quot;\.',	'\s+([.,?!:;])',	'\s*(&lt;[/]?span&gt;)\s*([.,?!:;])',	'([?!])+\.' ),
					( '$1$2',									'$1',				'.&quot;',		'$1', 				'$1$2',									'$1' )
				)
				)
			" />
			<!-- 																	.".=>." ??? -->
	</xsl:function>
	
	<xsl:function name="functx:replace-multi" as="xs:string?">
	  <xsl:param name="arg" as="xs:string?"/> 
	  <xsl:param name="changeFrom" as="xs:string*"/> 
	  <xsl:param name="changeTo" as="xs:string*"/> 
	 
	  <xsl:sequence select=" 
	   if (count($changeFrom) > 0)
	   then functx:replace-multi(
	          replace($arg, $changeFrom[1],
	                     functx:if-absent($changeTo[1],'')),
	          $changeFrom[position() > 1],
	          $changeTo[position() > 1])
	   else $arg
	 "/>
	   
	</xsl:function>

	<xsl:function name="functx:if-absent" as="item()*">
	  <xsl:param name="arg" as="item()*"/> 
	  <xsl:param name="value" as="item()*"/> 
	 
	  <xsl:sequence select=" 
	    if (exists($arg))
	    then $arg
	    else $value
	 "/>
	</xsl:function>
	
	<!-- JUS: Get reverse date if the input is not an empty sting. 
	The input YYYY-MM-TT is converted to TT.MM.YYYY. -->
	<xsl:function name="func:get_reverse_date">
		<xsl:param name="input_date" />
		<xsl:if test="$input_date[.!=''] ">
			<xsl:value-of select="concat(substring($input_date,9,2),'.',substring($input_date,6,2),'.',substring($input_date,1,4))" />
		</xsl:if>
	</xsl:function>
	
	<xsl:function name="func:getCitationStyleForJournal">
		<xsl:param name="idType"/>
		<xsl:param name="idValue"/>
		<xsl:value-of select="jfunc:getCitationStyleForJournal($idType,$idValue)"/>
	</xsl:function>
	
	<xsl:function name="func:substringAfterWhitespace">
		<xsl:param name="inputWithSpace"/>
		<xsl:value-of select="substring-after($inputWithSpace,' ')"/>
	</xsl:function>

</functions>